Import('env')
Import('common')

# Create an independent environment to run tests, so we don't
# propagate any changes back to the master env.
test_env = env.Clone()

# Make sure gtest is in the include path.
cpppath = test_env['CPPPATH']
# If cpppath is a single string instead of a list...
if hasattr(cpppath, 'lower'):
	cpppath = [cpppath]
cpppath.append('#/src/test/gmock/gtest/include')
cpppath.append('#/src/test/gmock/include')
cpppath.append('#/src/test/gmock')
cpppath.append('#/src/test/gmock/gtest')
test_env['CPPPATH'] = cpppath
Export('test_env')

# Build all .cpp files in this folder into a single binary.
sources = Glob('*.cpp')
sources.extend(Glob('gmock/gtest/src/gtest_main.cc'))

gtest = SConscript('gmock/gtest/src/sconscript', variant_dir='build/gtest', duplicate=0)
gmock = SConscript('gmock/src/sconscript', variant_dir='build/gmock', duplicate=0)

testrunner = test_env.Program(target='mtm-testrunner', source=sources, LIBS=[common, gmock, gtest, 'pthread'])

Return('testrunner')