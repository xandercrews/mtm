Import('env')
Import('base')
Import('domain')
Import('jsoncpp')
Import('zmq')

# Create an independent environment to run tests, so we don't
# propagate any changes back to the master env.
test_env = env.Clone()

# Be less picky about warnings and errors in this library, since it's not under
# our control.
test_env['CCFLAGS'] = test_env['CCFLAGS'].replace(' -Wmissing-field-initializers', '')

# Make sure gtest is in the include path.
cpppath = test_env['CPPPATH']
# If cpppath is a single string instead of a list...
if hasattr(cpppath, 'lower'):
	cpppath = [cpppath]
cpppath.append('#/src/test/gmock/gtest/include')
cpppath.append('#/src/test/gmock/include')
cpppath.append('#/src/test/gmock')
cpppath.append('#/src/test/gmock/gtest')
test_env['CPPPATH'] = cpppath
Export('test_env')

# Build all .cpp files in this folder into a single binary.
sources = Glob('*.cpp')
sources.extend(Glob('gmock/gtest/src/gtest_main.cc'))

gtest = SConscript('gmock/gtest/src/sconscript', variant_dir='build/gtest', duplicate=0)
gmock = SConscript('gmock/src/sconscript', variant_dir='build/gmock', duplicate=0)

libs = [base, domain, jsoncpp, zmq, gmock, gtest, 'pthread']
import platform
if platform.uname()[0] != 'Darwin':
    libs.append('rt')

testrunner = test_env.Program(target='nitro-testrunner', source=sources, 
    LIBS=libs)

Return('testrunner')
